plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}

ext {
    PUBLISH_ARTIFACT_ID = 'godot-openxr-vendors'
}

apply from: "../scripts/publish-module.gradle"

def khronosOpenxrLoaderAarDir = file("${buildDir}/temp/khronos_openxr_loader_aar")
def khronosOpenxrLoaderTargetDir = file("${buildDir}/temp/khronos_openxr_sdk")

def khronosOpenxrSourceDir = file("../thirdparty/openxr-source")
def khronosOpenxrValidationLayersAndroidLibraryTargetDir = file("${buildDir}/temp/openxr_validation_layers")
def khronosOpenxrValidationLayersAndroidAssetsTargetDir = file("${buildDir}/temp/openxr_validation_layers_assets")
def khronosOpenxrValidationLayersAndroidBuildDir = file("${buildDir}/temp/openxr_validation_layers_build")

def includeOpenxrValidationLayers = providers.gradleProperty("includeOpenxrValidationLayers")
    .map { it.toBoolean() }
    .getOrElse(false)

task extractKhronosOpenxrLoader {
    def khronosOpenxrLoader = configurations.detachedConfiguration(dependencies.create("org.khronos.openxr:openxr_loader_for_android:$versions.openxrVersion"))

    doLast {
        khronosOpenxrLoaderAarDir.mkdirs()

        copy {
            from zipTree(khronosOpenxrLoader.singleFile)
            into khronosOpenxrLoaderAarDir
        }
    }
}

task copyKhronosOpenxrLoader(dependsOn: extractKhronosOpenxrLoader) {
    def khronosOpenxrLoaderTargetDirArm64 = file("${khronosOpenxrLoaderTargetDir}/arm64-v8a/arm64-v8a")
    def khronosOpenxrLoaderTargetDirX86_64 = file("${khronosOpenxrLoaderTargetDir}/x86_64/x86_64")

    doLast {
        khronosOpenxrLoaderTargetDirArm64.mkdirs()
        khronosOpenxrLoaderTargetDirX86_64.mkdirs()

        copy {
            from "${khronosOpenxrLoaderAarDir}/prefab/modules/openxr_loader/libs/android.arm64-v8a/libopenxr_loader.so"
            into khronosOpenxrLoaderTargetDirArm64
        }
        copy {
            from "${khronosOpenxrLoaderAarDir}/prefab/modules/openxr_loader/libs/android.x86_64/libopenxr_loader.so"
            into khronosOpenxrLoaderTargetDirX86_64
        }
    }
}

task cleanKhronosOpenxrLoader(type: Delete) {
    delete khronosOpenxrLoaderAarDir
    delete khronosOpenxrLoaderTargetDir
}

android {
    compileSdk versions.compileSdk
    ndkVersion versions.ndkVersion

    defaultConfig {
        minSdk versions.minSdk
        targetSdk versions.targetSdk
        versionName getReleaseVersion()

        setProperty("archivesBaseName", "godotopenxr")

        ndk {
            //noinspection ChromeOsAbiSupport
            abiFilters "arm64-v8a"
        }
    }
    externalNativeBuild {
        cmake {
            path file('CMakeLists.txt')
        }
    }

    buildFeatures {
        buildConfig = true
    }

    namespace = "org.godotengine.openxr.vendors"

    flavorDimensions = ["vendor"]
    productFlavors {
        khronos {
            dimension "vendor"
            ndk {
                //noinspection ChromeOsAbiSupport
                abiFilters 'arm64-v8a', 'x86_64'
            }
        }
        lynx {
            dimension "vendor"
        }
        magicleap {
            dimension "vendor"
            ndk {
                //noinspection ChromeOsAbiSupport
                abiFilters 'arm64-v8a', 'x86_64'
            }
        }
        meta {
            dimension "vendor"
            ndk {
                //noinspection ChromeOsAbiSupport
                abiFilters 'arm64-v8a', 'x86_64'
            }
        }
        pico {
            dimension "vendor"
        }
    }

    sourceSets {
        debug.jniLibs.srcDirs += ['src/main/libs/debug/arm64-v8a']
        release.jniLibs.srcDirs += ['src/main/libs/release/arm64-v8a']

        khronos.jniLibs.srcDirs += [
                "${khronosOpenxrLoaderTargetDir}/arm64-v8a",
                "${khronosOpenxrLoaderTargetDir}/x86_64",
        ]
        khronosDebug.jniLibs.srcDirs += ['src/main/libs/debug/x86_64']
        khronosRelease.jniLibs.srcDirs += ['src/main/libs/release/x86_64']

        lynx.jniLibs.srcDirs += ["${khronosOpenxrLoaderTargetDir}/arm64-v8a"]

        magicleap.jniLibs.srcDirs += [
                "${khronosOpenxrLoaderTargetDir}/arm64-v8a",
                "${khronosOpenxrLoaderTargetDir}/x86_64",
        ]
        magicleapDebug.jniLibs.srcDirs += ['src/main/libs/debug/x86_64']
        magicleapRelease.jniLibs.srcDirs += ['src/main/libs/release/x86_64']

        meta.jniLibs.srcDirs += [
                "${khronosOpenxrLoaderTargetDir}/arm64-v8a",
                "${khronosOpenxrLoaderTargetDir}/x86_64",
        ]
        metaDebug.jniLibs.srcDirs += ['src/main/libs/debug/x86_64']
        metaRelease.jniLibs.srcDirs += ['src/main/libs/release/x86_64']

        pico.jniLibs.srcDirs += ["${khronosOpenxrLoaderTargetDir}/arm64-v8a"]

        if (includeOpenxrValidationLayers) {
            debug.jniLibs.srcDirs += ["${khronosOpenxrValidationLayersAndroidLibraryTargetDir}/arm64-v8a"]

            khronosDebug.jniLibs.srcDirs += ["${khronosOpenxrValidationLayersAndroidLibraryTargetDir}/x86_64"]
            magicleapDebug.jniLibs.srcDirs += ["${khronosOpenxrValidationLayersAndroidLibraryTargetDir}/x86_64"]
            metaDebug.jniLibs.srcDirs += ["${khronosOpenxrValidationLayersAndroidLibraryTargetDir}/x86_64"]

			// We don't need to give separate values for arm64-v8a and x86_64 - just one of them is fine.
            debug.assets.srcDirs += ["${khronosOpenxrValidationLayersAndroidAssetsTargetDir}/arm64-v8a"]
        }
    }

    packagingOptions {
        if (shouldNotStrip()) {
            doNotStrip '**/*.so'
        }
    }

    compileOptions {
        sourceCompatibility versions.javaVersion
        targetCompatibility versions.javaVersion
    }

    kotlinOptions {
        jvmTarget = versions.javaVersion
    }

    publishing {
        singleVariant("khronosDebug") {
            withSourcesJar()
            withJavadocJar()
        }
        singleVariant("khronosRelease") {
            withSourcesJar()
            withJavadocJar()
        }

        singleVariant("lynxDebug") {
            withSourcesJar()
            withJavadocJar()
        }
        singleVariant("lynxRelease") {
            withSourcesJar()
            withJavadocJar()
        }

        singleVariant("magicleapDebug") {
            withSourcesJar()
            withJavadocJar()
        }
        singleVariant("magicleapRelease") {
            withSourcesJar()
            withJavadocJar()
        }

        singleVariant("metaDebug") {
            withSourcesJar()
            withJavadocJar()
        }
        singleVariant("metaRelease") {
            withSourcesJar()
            withJavadocJar()
        }

        singleVariant("picoDebug") {
            withSourcesJar()
            withJavadocJar()
        }
        singleVariant("picoRelease") {
            withSourcesJar()
            withJavadocJar()
        }
    }

    tasks.matching { it.name.contains("CMake") || it.name.contains("externalNativeBuild") }.all { task ->
        def taskPrefix = ""
        if (project.path != ":") {
            taskPrefix = project.path + ":"
        }

        // Disable the externalNativeBuild* and *CMake* tasks as they would cause build failures since
        // the cmake build files are only setup for editing support.
        gradle.startParameter.excludedTaskNames += taskPrefix + task.name
    }
}

dependencies {
    compileOnly libraries.godotAndroidLib

    implementation "androidx.fragment:fragment:$versions.fragmentVersion"
    implementation "androidx.core:core-splashscreen:$versions.splashscreenVersion"
}

task buildOpenxrValidationLayersAndroid {
    for (abi in ['arm64-v8a', 'x86_64']) {
        def suffix = (abi == 'arm64-v8a') ? 'Arm64' : (abi == 'x86_64') ? 'X86_64' : abi

        def libraryTargetDir = file("${khronosOpenxrValidationLayersAndroidLibraryTargetDir}/${abi}/${abi}")
        def jsonTargetDir = file("${khronosOpenxrValidationLayersAndroidAssetsTargetDir}/${abi}/openxr/1/api_layers/implicit.d")
        def buildDir = file("${khronosOpenxrValidationLayersAndroidBuildDir}/${abi}")

        tasks.create(name: "configureOpenxrValidationLayersAndroid${suffix}", type: Exec) {
            doFirst {
                buildDir.mkdirs()
            }
            workingDir buildDir
            commandLine [
                "cmake",
                "${khronosOpenxrSourceDir}",
                "-DCMAKE_SYSTEM_NAME=Android",
                "-DCMAKE_SYSTEM_VERSION=${versions.targetSdk}",
                "-DCMAKE_ANDROID_ARCH_ABI=${abi}",
                "-DCMAKE_ANDROID_NDK=${android.ndkDirectory}",
                "-DCMAKE_BUILD_TYPE=Release",
                "-DBUILD_TESTS=OFF",
                "-DBUILD_CONFORMANCE_TESTS=OFF",
                "-DBUILD_LOADER=OFF",
                "-DBUILD_API_LAYERS=YES",
            ]
        }

        tasks.create(name: "buildOpenxrValidationLayersAndroid${suffix}", type: Exec) {
            workingDir buildDir
            commandLine "cmake", "--build", "."
            dependsOn "configureOpenxrValidationLayersAndroid${suffix}"
        }

        tasks.create(name: "copyOpenxrValidationLayersAndroid${suffix}Library", type: Copy) {
            doFirst {
                libraryTargetDir.mkdirs()
            }
            from("${buildDir}/src/api_layers") {
                include 'libXrApiLayer_core_validation.so', 'libXrApiLayer_best_practices_validation.so'
            }
            into libraryTargetDir
            dependsOn "buildOpenxrValidationLayersAndroid${suffix}"
        }

        tasks.create(name: "copyOpenxrValidationLayersAndroid${suffix}JSON", type: Copy) {
            doFirst {
                jsonTargetDir.mkdirs()
            }
            from("${buildDir}/src/api_layers") {
                include 'XrApiLayer_core_validation.json', 'XrApiLayer_best_practices_validation.json'
            }
            into jsonTargetDir
            dependsOn "buildOpenxrValidationLayersAndroid${suffix}"
        }
    }

    dependsOn "copyOpenxrValidationLayersAndroidArm64Library"
    dependsOn "copyOpenxrValidationLayersAndroidArm64JSON"

    dependsOn "copyOpenxrValidationLayersAndroidX86_64Library"
    dependsOn "copyOpenxrValidationLayersAndroidX86_64JSON"
}

task cleanOpenxrValidationLayersAndroid(type: Delete) {
    delete khronosOpenxrValidationLayersAndroidLibraryTargetDir
    delete khronosOpenxrValidationLayersAndroidAssetsTargetDir
    delete khronosOpenxrValidationLayersAndroidBuildDir
}

task cleanAssets(type: Delete) {
    // Delete the 'addons' directory in the 'assets' folder
    delete("src/main/assets/addons")
}

task cleanCxx(type: Delete) {
    delete(".cxx")
}

task copyDebugAARToAddons(type: Copy) {
    from 'build/outputs/aar'
    include 'godotopenxr-*-debug.aar'
    into '../demo/addons/godotopenxrvendors/.bin/android/debug'
}

task copyReleaseAARToAddons(type: Copy) {
    from 'build/outputs/aar'
    include 'godotopenxr-*-release.aar'
    into '../demo/addons/godotopenxrvendors/.bin/android/release'
}

task copyGdExtensionConfigToAssets(type: Copy) {
    description 'Copy plugin.gdextension from the addons directory to the assets directory'

    from '../demo/addons/godotopenxrvendors/'
    include 'plugin.gdextension'
    into 'src/main/assets/addons/godotopenxrvendors/'
}

preBuild.dependsOn(copyGdExtensionConfigToAssets)
assemble.dependsOn(copyGdExtensionConfigToAssets)
preBuild.dependsOn(copyKhronosOpenxrLoader)
assemble.dependsOn(copyKhronosOpenxrLoader)
if (includeOpenxrValidationLayers) {
    preBuild.dependsOn(buildOpenxrValidationLayersAndroid)
    assemble.dependsOn(buildOpenxrValidationLayersAndroid)
}
assemble.finalizedBy(copyDebugAARToAddons)
assemble.finalizedBy(copyReleaseAARToAddons)
clean.dependsOn(cleanAssets)
clean.dependsOn(cleanCxx)
clean.dependsOn(cleanKhronosOpenxrLoader)
clean.dependsOn(cleanOpenxrValidationLayersAndroid)
