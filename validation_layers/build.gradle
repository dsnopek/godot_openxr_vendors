plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}

def khronosOpenxrSourceDir = file("../thirdparty/openxr-source")
def khronosOpenxrValidationLayersAndroidLibraryTargetDir = file("${buildDir}/temp/openxr_validation_layers")
def khronosOpenxrValidationLayersAndroidAssetsTargetDir = file("${buildDir}/temp/openxr_validation_layers_assets")
def khronosOpenxrValidationLayersAndroidBuildDir = file("${buildDir}/temp/openxr_validation_layers_build")

android {
    compileSdk versions.compileSdk
    ndkVersion versions.ndkVersion

    defaultConfig {
        minSdk versions.minSdk
        targetSdk versions.targetSdk
        versionName getReleaseVersion()

        setProperty("archivesBaseName", "openxr-validation-layers")

        ndk {
            //noinspection ChromeOsAbiSupport
            abiFilters "arm64-v8a", "x86_64"
        }
    }

    namespace = "org.godotengine.openxr.validation_layers"

    sourceSets {
        main {
            jniLibs.srcDirs += [
                "${khronosOpenxrValidationLayersAndroidLibraryTargetDir}/arm64-v8a",
                "${khronosOpenxrValidationLayersAndroidLibraryTargetDir}/x86_64",
            ]

            // We don't need to give separate values for arm64-v8a and x86_64 - just one of them is fine.
            assets.srcDirs += ["${khronosOpenxrValidationLayersAndroidAssetsTargetDir}/arm64-v8a"]
        }
    }

    packagingOptions {
        if (shouldNotStrip()) {
            doNotStrip '**/*.so'
        }
    }

    compileOptions {
        sourceCompatibility versions.javaVersion
        targetCompatibility versions.javaVersion
    }

    kotlinOptions {
        jvmTarget = versions.javaVersion
    }
}

task buildOpenxrValidationLayersAndroid {
    for (abi in ['arm64-v8a', 'x86_64']) {
        def suffix = (abi == 'arm64-v8a') ? 'Arm64' : (abi == 'x86_64') ? 'X86_64' : abi

        def libraryTargetDir = file("${khronosOpenxrValidationLayersAndroidLibraryTargetDir}/${abi}/${abi}")
        def jsonTargetDir = file("${khronosOpenxrValidationLayersAndroidAssetsTargetDir}/${abi}/openxr/1/api_layers/implicit.d")
        def buildDir = file("${khronosOpenxrValidationLayersAndroidBuildDir}/${abi}")

        tasks.create(name: "configureOpenxrValidationLayersAndroid${suffix}", type: Exec) {
            doFirst {
                buildDir.mkdirs()
            }
            workingDir buildDir
            commandLine [
                "cmake",
                "${khronosOpenxrSourceDir}",
                "-DCMAKE_SYSTEM_NAME=Android",
                "-DCMAKE_SYSTEM_VERSION=${versions.targetSdk}",
                "-DCMAKE_ANDROID_ARCH_ABI=${abi}",
                "-DCMAKE_ANDROID_NDK=${android.ndkDirectory}",
                "-DCMAKE_BUILD_TYPE=Release",
                "-DBUILD_TESTS=OFF",
                "-DBUILD_CONFORMANCE_TESTS=OFF",
                "-DBUILD_LOADER=OFF",
                "-DBUILD_API_LAYERS=YES",
                // CMake may not detect OpenGL ES support, so we force it to be on.
                "-DCMAKE_C_FLAGS=-DXR_USE_GRAPHICS_API_OPENGL_ES",
                "-DCMAKE_CXX_FLAGS=-DXR_USE_GRAPHICS_API_OPENGL_ES",
            ]
        }

        tasks.create(name: "buildOpenxrValidationLayersAndroid${suffix}", type: Exec) {
            workingDir buildDir
            commandLine "cmake", "--build", ".", "--verbose"
            dependsOn "configureOpenxrValidationLayersAndroid${suffix}"
        }

        tasks.create(name: "copyOpenxrValidationLayersAndroid${suffix}Library", type: Copy) {
            doFirst {
                libraryTargetDir.mkdirs()
            }
            from("${buildDir}/src/api_layers") {
                include 'libXrApiLayer_core_validation.so', 'libXrApiLayer_best_practices_validation.so'
            }
            into libraryTargetDir
            dependsOn "buildOpenxrValidationLayersAndroid${suffix}"
        }

        tasks.create(name: "copyOpenxrValidationLayersAndroid${suffix}JSON", type: Copy) {
            doFirst {
                jsonTargetDir.mkdirs()
            }
            from("${buildDir}/src/api_layers") {
                include 'XrApiLayer_core_validation.json', 'XrApiLayer_best_practices_validation.json'
            }
            into jsonTargetDir
            dependsOn "buildOpenxrValidationLayersAndroid${suffix}"
        }
    }

    dependsOn "copyOpenxrValidationLayersAndroidArm64Library"
    dependsOn "copyOpenxrValidationLayersAndroidArm64JSON"

    dependsOn "copyOpenxrValidationLayersAndroidX86_64Library"
    dependsOn "copyOpenxrValidationLayersAndroidX86_64JSON"
}

task copyDebugAARToAddons(type: Copy) {
    from 'build/outputs/aar'
    include 'openxr-validation-layers-debug.aar'
    into '../demo/addons/godotopenxrvendors/.bin/android/debug'
}

task copyReleaseAARToAddons(type: Copy) {
    from 'build/outputs/aar'
    include 'openxr-validation-layers-release.aar'
    into '../demo/addons/godotopenxrvendors/.bin/android/release'
}

task cleanOpenxrValidationLayersAndroid(type: Delete) {
    delete khronosOpenxrValidationLayersAndroidLibraryTargetDir
    delete khronosOpenxrValidationLayersAndroidAssetsTargetDir
    delete khronosOpenxrValidationLayersAndroidBuildDir
}

preBuild.dependsOn(buildOpenxrValidationLayersAndroid)
assemble.dependsOn(buildOpenxrValidationLayersAndroid)
assemble.finalizedBy(copyDebugAARToAddons)
assemble.finalizedBy(copyReleaseAARToAddons)
clean.dependsOn(cleanOpenxrValidationLayersAndroid)
